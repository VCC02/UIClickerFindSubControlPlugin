{
    Copyright (C) 2026 VCC
    creation date: 18 Feb 2025
    initial release date: 23 Mar 2025

    author: VCC
    Permission is hereby granted, free of charge, to any person obtaining a copy
    of this software and associated documentation files (the "Software"),
    to deal in the Software without restriction, including without limitation
    the rights to use, copy, modify, merge, publish, distribute, sublicense,
    and/or sell copies of the Software, and to permit persons to whom the
    Software is furnished to do so, subject to the following conditions:
    The above copyright notice and this permission notice shall be included
    in all copies or substantial portions of the Software.
    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
    EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
    MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
    DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
    TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE
    OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
}


library UIClickerFindSubControl;

{$mode objfpc}{$H+}

uses
  Windows,
  Interfaces, Classes, SysUtils, Graphics, FindSubControlDM, ImgList,
  ClickerActionPlugins, ClickerUtils, DllUtils, ClickerActionPluginAccess,
  FindSubControlPluginProperties, ClickerActionProperties,
  ctypes, CLHeaders, BitmapProcessingGPU
  ;


var
  AddToLogCallback: TOnActionPlugin_AddToLog;



procedure AddToLog(AMsg: string);  //thread safe
begin
  if @AddToLogCallback <> nil then
    AddToLogCallback(DefaultPluginReference, @AMsg[1]);
end;


function GetAPIVersion: DWord; cdecl;
begin
  Result := CActionPlugin_APIVersion;
end;


procedure GetListOfProperties(APluginReference: Pointer;                 //UIClicker passes the plugin reference to the plugin, then the plugin calls some callbacks with that reference
                              AListOfProperties: Pointer;
                              AListOfPropertiesLen: PDWord;
                              AOnActionPlugin_UpdatePropertyIcons: TOnActionPlugin_UpdatePropertyIcons); cdecl;
var
  Properties: string;
begin
  Properties := FillInPropertyDetails;
  AListOfPropertiesLen^ := SetPointedContentFromString(Properties, AListOfProperties);

  dmProperties := TdmProperties.Create(nil);
  try
    UpdatePropertyIcons(APluginReference, AOnActionPlugin_UpdatePropertyIcons, dmProperties.imglstProperties, CPropertiesCount);
  finally
    dmProperties.Free;
  end;
end;


procedure DrawWipeRect(ACanvas: TCanvas; NewWidth, NewHeight: Integer);
begin
  ACanvas.Lock;
  try
    ACanvas.Brush.Style := bsSolid;
    ACanvas.Brush.Color := clWhite;
    ACanvas.Pen.Color := clWhite;
    ACanvas.Rectangle(0, 0, NewWidth {- 1}, NewHeight {- 1});
  finally
    ACanvas.Unlock
  end;
end;


procedure WipeBitmap(ABitmap: TBitmap; NewWidth, NewHeight: Integer);
begin
  //ABitmap.Clear;
  ABitmap.SetSize(NewWidth, NewHeight);
  DrawWipeRect(ABitmap.Canvas, NewWidth, NewHeight);
end;


function ExecutePlugin(APluginReference: Pointer;
                       AListOfPluginSettings: Pointer;
                       AListOfPluginSettingsLen: PDWord;

                       AOnActionPlugin_GetActionCount: TOnActionPlugin_GetActionCount;
                       AOnActionPlugin_GetActionInfoByIndex: TOnActionPlugin_GetActionInfoByIndex;
                       AOnActionPlugin_GetActionContentByIndex: TOnActionPlugin_GetActionContentByIndex;
                       AOnActionPlugin_ExecuteAction: TOnActionPlugin_ExecuteAction;
                       AOnActionPlugin_GetAllTemplateVars: TOnActionPlugin_GetAllTemplateVars;
                       AOnActionPlugin_SetTemplateVar: TOnActionPlugin_SetTemplateVar;
                       AOnActionPlugin_DebugPoint: TOnActionPlugin_DebugPoint;
                       AOnActionPlugin_AddToLog: TOnActionPlugin_AddToLog;
                       AOnActionPlugin_SetResultImg: TOnActionPlugin_SetResultImg;
                       AOnActionPlugin_LoadBitmap: TOnActionPlugin_LoadBitmap;
                       AOnActionPlugin_GetAllowedFilesInfo: TOnActionPlugin_GetAllowedFilesInfo;
                       AOnActionPlugin_SetBitmap: TOnActionPlugin_SetBitmap;
                       AOnActionPlugin_Screenshot: TOnActionPlugin_Screenshot;
                       AOnActionPlugin_CheckStopAllActionsOnDemand: TOnActionPlugin_CheckStopAllActionsOnDemand;
                       AOnActionPlugin_InMemFS: TOnActionPlugin_InMemFS;
                       AOnActionPlugin_AppProcMsg: TOnActionPlugin_AppProcMsg
                       ): Boolean; cdecl;
var
  ActionCount: Integer;
  PluginSettingsStr, AllVarsStr: string;
  ListOfAllVars: TStringList;
  PluginSettings: TStringList;

  procedure SetTemplateVar(AVarName, AVarValue: string);
  begin
    AOnActionPlugin_SetTemplateVar(APluginReference, @AVarName[1], @AVarValue[1]);
  end;

  procedure GetAllTemplateVars(out AAllTemplateVars: string);  //AAllTemplateVars are encoded as CRLF separated key=value strings, ready to be used on a TStringlist
  var
    Buffer: string;
    AllTemplateVarsPtr: Pointer;
    NameLengthDWord: DWord;
  begin
    SetLength(Buffer, CMaxSharedStringLength + 1);
    AllTemplateVarsPtr := @Buffer[1];
    NameLengthDWord := 0;
    AAllTemplateVars := '';

    AOnActionPlugin_GetAllTemplateVars(APluginReference, AllTemplateVarsPtr, @NameLengthDWord);  //decode action execution results

    SetPointedContentToString(AllTemplateVarsPtr, AAllTemplateVars);
    SetLength(AAllTemplateVars, NameLengthDWord);
  end;

  function GetPluginSettings: string;
  begin
    SetLength(Result, CMaxSharedStringLength);
    SetPointedContentToString(AListOfPluginSettings, Result);
    SetLength(Result, AListOfPluginSettingsLen^);
  end;


  procedure ExecFindSubControlOnGPU(APlatformIndex, ADeviceIndex: Integer);
  var
    SourceBitmap, SubBitmap: TBitmap;
    ColorErrorLevel: Integer;
    SubCnvXOffset, SubCnvYOffset: Integer;
    AFoundBitmaps: TCompRecArr;
    TotalErrorCount, FastSearchColorErrorCount: Integer;
    AUseFastSearch, AIgnoreBackgroundColor, AGetAllBitmaps: Boolean;
    ABackgroundColor: TColor;
    AIgnoredColorsArr: TColorArr;
    ASleepySearch: Byte;
    AOutsideTickCount, APrecisionTimeout: QWord;
    AThreadCount: Integer;
    AResultedErrorCount: Integer;
    AStopSearchOnDemand: PBoolean;
    StopSearchOnMismatch: Boolean;

    SourceStream, SubStream: TMemoryStream;
    i: Integer;
    BytesPerPixelSrc, BytesPerPixelSub: Integer;
    ScLn: Pointer;
    MatchResult: Boolean;
    DebuggingInfo: string;
    AGPUDbgBuffer: TIntArr;
  begin
    AStopSearchOnDemand := nil;
    SetTemplateVar('$MatchResult$', 'not executed');

    SourceBitmap := TBitmap.Create;
    SubBitmap := TBitmap.Create;
    SourceStream := TMemoryStream.Create;
    SubStream := TMemoryStream.Create;
    try
      WipeBitmap(SourceBitmap, 300, 400);
      WipeBitmap(SubBitmap, 30, 40);

      SourceBitmap.Canvas.Font.Color := clBlack;
      SourceBitmap.Canvas.TextOut(21, 17, 'abc');
      SubBitmap.Canvas.Font.Color := clBlack;
      SubBitmap.Canvas.TextOut(0, 0, 'abc');
      StopSearchOnMismatch := True;
      ASleepySearch := 0;
      ColorErrorLevel := 10;
      TotalErrorCount := 10;
      FastSearchColorErrorCount := 5;
      SubCnvXOffset := -1;
      SubCnvYOffset := -1;
      AUseFastSearch := True;
      AIgnoreBackgroundColor := False;
      AGetAllBitmaps := False;
      ABackgroundColor := clWhite;   //not used    //clFuchsia
      SetLength(AIgnoredColorsArr, 0);

      if SourceBitmap.PixelFormat = pf24bit then
        BytesPerPixelSrc := 3
      else
        BytesPerPixelSrc := 4;

      if SubBitmap.PixelFormat = pf24bit then
        BytesPerPixelSub := 3
      else
        BytesPerPixelSub := 4;

      for i := 0 to SourceBitmap.Height - 1 do
      begin
        ScLn := SourceBitmap.{%H-}ScanLine[i];
        SourceStream.Write(ScLn^, SourceBitmap.Width * BytesPerPixelSrc);
      end;

      for i := 0 to SubBitmap.Height - 1 do
      begin
        ScLn := SubBitmap.{%H-}ScanLine[i];
        SubStream.Write(ScLn^, SubBitmap.Width * BytesPerPixelSub);
      end;

      AOutsideTickCount := GetTickCount64;
      APrecisionTimeout := 15000;
      MatchResult := BitmapPosMatch_BruteForceOnGPU(SourceStream.Memory,
                                                    SubStream.Memory,
                                                    BytesPerPixelSrc,
                                                    BytesPerPixelSub,
                                                    SourceBitmap.Width,
                                                    SourceBitmap.Height,
                                                    SubBitmap.Width,
                                                    SubBitmap.Height,
                                                    ColorErrorLevel,
                                                    SubCnvXOffset,
                                                    SubCnvYOffset,
                                                    AFoundBitmaps,
                                                    TotalErrorCount,
                                                    FastSearchColorErrorCount,
                                                    AUseFastSearch,
                                                    AIgnoreBackgroundColor,
                                                    AGetAllBitmaps,
                                                    ABackgroundColor,
                                                    AIgnoredColorsArr,
                                                    ASleepySearch,
                                                    AOutsideTickCount,
                                                    APrecisionTimeout,
                                                    AThreadCount,
                                                    '', //CustomOpenCLPath
                                                    APlatformIndex,
                                                    ADeviceIndex,
                                                    eaOpenCL3Only,
                                                    True, //include -g
                                                    False, //SlaveQueueFromDevice
                                                    False, //UseAllKernelsEvent
                                                    False, //NdrangeNoLocalParam
                                                    False, //UseEventsInEnqueueKernel
                                                    True, //WaitForAllKernelsToBeDone
                                                    False, //ReleaseFinalEventAtkernelEnd
                                                    True, //IgnoreExecutionAvailability
                                                    AGPUDbgBuffer,
                                                    AResultedErrorCount,
                                                    AStopSearchOnDemand,
                                                    StopSearchOnMismatch);
    finally
      SourceStream.Free;
      SubStream.Free;
      SourceBitmap.Free;
      SubBitmap.Free;
    end;

    SetTemplateVar('$MatchResult$', BoolToStr(MatchResult, 'True', 'False'));
    SetTemplateVar('$MatchDuration$', IntToStr(GetTickCount64 - AOutsideTickCount));
    SetTemplateVar('$MatchSubCnvXOffset$', IntToStr(SubCnvXOffset));    //20 + 2
    SetTemplateVar('$MatchSubCnvYOffset$', IntToStr(SubCnvYOffset));    //16 + 3
    SetTemplateVar('$PluginDebuggingInfo$', DebuggingInfo);
  end; //ExecFindSubControlOnGPU

  procedure ExecFindSubControlOnPlugin(APlatformIndex, ADeviceIndex: Integer; var AFindSubControlOptions: TClkFindSubControlOptions; var AActionOptions: TClkActionOptions);
  begin
    //ToDo move FindSubControl stuff from ClickerActionExecution.pas into a new unit,
    //which can be included in this project, then call ExecuteFindSubControlAction

    //ToDo: replace the following code with ExecuteFindSubControlAction
    if AFindSubControlOptions.MatchBitmapAlgorithm = mbaBruteForceOnGPU then
      ExecFindSubControlOnGPU(APlatformIndex, ADeviceIndex)
    else
      AddToLog('FindSubControl not implemented yet.');
  end;

  procedure GetCLInfo;
  var
    OpenCLDll: TOpenCL;

    procedure LogCallResult(AError: Integer; AFuncName, AInfo: string);
    begin
      //call some event
      if AError <> 0 then
        raise Exception.Create('Error ' + CLErrorToStr(AError) + ' instead of "' + AInfo + '" at "' + AFuncName + '" OpenCL API call.');
    end;

    procedure GetPlatformInfo(APlatformIndex: Integer; APlatform: cl_platform_id; APlatformParamName: cl_platform_info; APlatformParamNameStr: string);
    var
      InfoLen: csize_t;
      Info: string;
      Error: Integer;
    begin
      SetLength(Info, 32768);
      Error := OpenCLDll.clGetPlatformInfo(APlatform, APlatformParamName, csize_t(Length(Info)), @Info[1], InfoLen);
      LogCallResult(Error, 'clGetPlatformInfo(' + APlatformParamNameStr + ')', '');
      SetLength(Info, InfoLen);
      SetTemplateVar('$CL_' + APlatformParamNameStr + '[' + IntToStr(APlatformIndex) + ']$', Info);
    end;

    procedure GetDeviceInfo(APlatformIndex, ADeviceIndex: Integer; ADevice: cl_Device_id; ADeviceParamName: cl_device_info; ADeviceParamNameStr: string; ACLParamIsInt: Boolean = False);
    var
      InfoLen: csize_t;
      Info: string;
      Error: Integer;
    begin
      SetLength(Info, 32768);
      Error := OpenCLDll.clGetDeviceInfo(ADevice, ADeviceParamName, csize_t(Length(Info)), @Info[1], InfoLen);
      LogCallResult(Error, 'clGetDeviceInfo(' + ADeviceParamNameStr + ')', '');
      SetLength(Info, InfoLen);

      if ACLParamIsInt then
        Info := IntToStr(PDWord(@Info[1])^);

      SetTemplateVar('$CL_' + ADeviceParamNameStr + '[' + IntToStr(APlatformIndex) + ', ' + IntToStr(ADeviceIndex) + ']$', Info);
    end;

  var
    Error: Integer;
    PlatformCount, DeviceCount: cl_uint;
    PlatformIDs: Pcl_platform_id;
    DeviceIDs: Pcl_device_id;
    i, j: Integer;
  begin
    OpenCLDll := TOpenCL.Create;
    try
      if not OpenCLDll.Loaded then
        raise Exception.Create('OpenCL not available. The dll is expected to exist at ' + OpenCLDll.ExpectedDllLocation);

      Error := OpenCLDll.clGetPlatformIDs(0, nil, @PlatformCount);
      LogCallResult(Error, 'clGetPlatformIDs', 'PlatformCount: ' + IntToStr(PlatformCount));
      SetTemplateVar('$CL_PlatformCount$', IntToStr(PlatformCount));

      GetMem(PlatformIDs, PlatformCount * SizeOf(cl_platform_id));
      try
        Error := OpenCLDll.clGetPlatformIDs(PlatformCount, PlatformIDs, nil);
        LogCallResult(Error, 'clGetPlatformIDs', '');

        for i := 0 to PlatformCount - 1 do
        begin
          GetPlatformInfo(i, PlatformIDs[i], CL_PLATFORM_PROFILE, 'PlatformProfile');
          GetPlatformInfo(i, PlatformIDs[i], CL_PLATFORM_VERSION, 'PlatformVersion');
          GetPlatformInfo(i, PlatformIDs[i], CL_PLATFORM_NAME, 'PlatformName');
          GetPlatformInfo(i, PlatformIDs[i], CL_PLATFORM_VENDOR, 'PlatformVendor');
          GetPlatformInfo(i, PlatformIDs[i], CL_PLATFORM_EXTENSIONS, 'PlatformExtensions');

          Error := OpenCLDll.clGetDeviceIDs(PlatformIDs[i], CL_DEVICE_TYPE_GPU, 0, nil, @DeviceCount);
          LogCallResult(Error, 'clGetDeviceIDs', 'DeviceCount: ' + IntToStr(DeviceCount));
          SetTemplateVar('$CL_DeviceCount$', IntToStr(DeviceCount));

          GetMem(DeviceIDs, DeviceCount * SizeOf(cl_device_id));
          try
            Error := OpenCLDll.clGetDeviceIDs(PlatformIDs[i], CL_DEVICE_TYPE_GPU, DeviceCount, DeviceIDs, nil);
            LogCallResult(Error, 'clGetDeviceIDs', '');

            for j := 0 to DeviceCount - 1 do
            begin
              GetDeviceInfo(i, j, DeviceIDs[j], CL_DEVICE_NAME, 'DeviceName');
              GetDeviceInfo(i, j, DeviceIDs[j], CL_DEVICE_VENDOR, 'DeviceVendor');
              GetDeviceInfo(i, j, DeviceIDs[j], CL_DEVICE_VERSION, 'DeviceVersion');
              GetDeviceInfo(i, j, DeviceIDs[j], CL_DEVICE_PROFILE, 'DeviceProfile');
              GetDeviceInfo(i, j, DeviceIDs[j], CL_DEVICE_OPENCL_C_VERSION, 'DeviceOpenCLCVersion');
              GetDeviceInfo(i, j, DeviceIDs[j], CL_DEVICE_EXTENSIONS, 'DeviceExtensions');

              GetDeviceInfo(i, j, DeviceIDs[j], CL_DEVICE_TYPE_INFO, 'DeviceTypeInfo', True);    //CL_DEVICE_TYPE_GPU
              GetDeviceInfo(i, j, DeviceIDs[j], CL_DEVICE_GLOBAL_MEM_SIZE, 'DeviceGlobalMemSize', True);
              GetDeviceInfo(i, j, DeviceIDs[j], CL_DEVICE_IMAGE_SUPPORT, 'DeviceImageSupport', True);
              GetDeviceInfo(i, j, DeviceIDs[j], CL_DEVICE_LOCAL_MEM_SIZE, 'DeviceLocalMemSize', True);
              GetDeviceInfo(i, j, DeviceIDs[j], CL_DEVICE_AVAILABLE, 'DeviceAvailable', True);
              GetDeviceInfo(i, j, DeviceIDs[j], CL_DEVICE_COMPILER_AVAILABLE, 'DeviceCompilerAvailable', True);
              GetDeviceInfo(i, j, DeviceIDs[j], CL_DEVICE_EXECUTION_CAPABILITIES, 'DeviceExecutionCapabilities', True);
            end;
          finally
            Freemem(DeviceIDs, DeviceCount * SizeOf(cl_device_id));
          end;
        end;
      finally
        Freemem(PlatformIDs, PlatformCount * SizeOf(cl_platform_id));
      end;
    finally
      OpenCLDll.Free;
    end;

    SetTemplateVar('$CL_RequiredExtensionsFor_queue_t$', '__opencl_c_device_enqueue, __opencl_c_generic_address_space, __opencl_c_program_scope_global_variables (OpenCLC2.0)');
    SetTemplateVar('$CL_RequiredExtensionsFor_image1d_t$', '__opencl_c_images (OpenCLC3.0)');
  end;

var
  FindSubControlActionPropertiesStr: string;
  ListOfFindSubControlActionProperties: TStringList;
  FindSubControlOptions: TClkFindSubControlOptions;
  FindSubControlActionOptions: TClkActionOptions;
  PlatformIndex, DeviceIndex: Integer;
begin
  Result := True;
  try
    DefaultOnActionPlugin_DebugPoint := AOnActionPlugin_DebugPoint;
    DefaultPluginReference := APluginReference;
    AddToLogCallback := AOnActionPlugin_AddToLog;

    ListOfAllVars := TStringList.Create;
    try
      PluginSettings := TStringList.Create;
      try
        if not DbgPoint('Entering ExecutePlugin', '') then     //see ClkDbgSym.ppr for expected statement formats
          Exit;

        ActionCount := AOnActionPlugin_GetActionCount(APluginReference);
        if ActionCount = 0 then
        begin
          SetTemplateVar(CActionPlugin_ExecutionResultErrorVar, 'This plugin does not run on an empty template.');
          Exit;
        end;

        PluginSettingsStr := GetPluginSettings;
        PluginSettings.Text := PluginSettingsStr;

        if not DbgPoint('Second debug point', '') then     //see ClkDbgSym.ppr for expected statement formats
          Exit;

        if PluginSettings.Count <> CPropertiesCount then
        begin
          SetTemplateVar(CActionPlugin_ExecutionResultErrorVar, 'Cannot get the list of plugin properties.');
          Exit;
        end;

        GetAllTemplateVars(AllVarsStr);
        ListOfAllVars.Text := AllVarsStr;

        if PluginSettings.Values[COperationModePropertyName] = COperationMode_GetCapabilities_Name then
        begin
          GetCLInfo;
          Exit;
        end;

        if PluginSettings.Values[COperationModePropertyName] = COperationMode_Execute_Name then
        begin
          GetAllTemplateVars(AllVarsStr);
          ListOfAllVars.Text := AllVarsStr;
          FindSubControlActionPropertiesStr := EvaluateAllReplacements(ListOfAllVars, PluginSettings.Values[CFindSubControlActionPropertiesVarPropertyName]);
          ListOfFindSubControlActionProperties := TStringList.Create;
          try
            ListOfFindSubControlActionProperties.Text := StringReplace(FindSubControlActionPropertiesStr, '&', #13#10, [rfReplaceAll]);
            SetFindSubControlActionProperties(ListOfFindSubControlActionProperties, nil, FindSubControlOptions, FindSubControlActionOptions);
          finally
            ListOfFindSubControlActionProperties.Free;
          end;

          PlatformIndex := StrToIntDef(PluginSettings.Values[CPlatformIndexPropertyName], -1);
          DeviceIndex := StrToIntDef(PluginSettings.Values[CDeviceIndexPropertyName], -1);

          ExecFindSubControlOnPlugin(PlatformIndex, DeviceIndex, FindSubControlOptions, FindSubControlActionOptions);
        end;

      finally
        PluginSettings.Free;
      end;
    finally
      ListOfAllVars.Free;
    end;
  except
    on E: Exception do
    begin
      Result := False;
      SetTemplateVar(CActionPlugin_ExecutionResultErrorVar, 'Plugin ex: ' + E.Message);
    end;
  end;
end;


exports
  GetAPIVersion,
  GetListOfProperties,
  ExecutePlugin;

begin
end.

